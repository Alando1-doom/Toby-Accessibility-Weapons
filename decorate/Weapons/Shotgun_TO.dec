ACTOR Shotgun_TO : DoomWeapon
{
    Weapon.SelectionOrder 1300
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
    Weapon.AmmoType "Shell"
    Weapon.SlotNumber 3
    AttackSound "weapons/shotgf"
    Weapon.UpSound "weapons/shotup"
    Inventory.PickupSound "weapons/shotgun_pkup"
    Inventory.PickupMessage "$GOTSHOTGUN"
    Obituary "$OB_MPSHOTGUN"
    Tag "$TAG_SHOTGUN"
    Decal BulletChip
    Scale 0.85
    States
    {
    Ready:
        SHTG A 1 A_WeaponReady
        Loop
    Deselect:
        SHTG A 1 A_Lower
        Loop
    Select:
        SHTG A 1 A_Raise
        Loop
    Fire:
        SHTG A 3 A_GunFlash
        SHTG A 7 A_FireBullets(4,3,8,5,"BulletPuff")
        SHTG BCDEFGHIJKLM 1
        SHTG LKJIHGFEDCB 1
        SHTG A 2
        SHTG A 7 A_ReFire
        Goto Ready
    Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone
    Spawn:
		SHOT A 1 A_SpawnItemEx("ShotgunPickupChecker", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        SHOT AA 25 
        Loop
    }
}


ACTOR Shotgun_Giver : CustomInventory replaces Shotgun
{
	Game Doom
    SpawnID 27
    Inventory.PickupMessage "$GOTSHOTGUN"
	Inventory.RespawnTics 105
	Inventory.PickupSound ""
	Scale 0.85
    States
    {
    Spawn:
        SHOT A 1 A_SpawnItemEx("ShotgunPickupChecker", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        SHOT AA 25 
        Loop
    Pickup:
		TNT1 A 1 A_JumpIfInventory("Shotgun_TO", 1, "HasShotgun")
        TNT1 A 1 A_GiveInventory("Shotgun_TO", 1)
		TNT1 A 1 A_PlaySoundEx("weapons/shotgun_pkup", "Auto", 0, -1)
        Stop
	HasShotgun:
		TNT1 A 1 A_JumpIfInventory("Backpack_TO", 1, "HasBP")
        TNT1 A 1 A_JumpIfInventory("Shell", 50, "NoPickup")
        TNT1 A 1 A_GiveInventory("Shell", 4)
		TNT1 A 1 A_PlaySoundEx("weapons/shotgun_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/shotgun", "Auto", 0, -1)
		Stop
    HasBP:
        TNT1 A 1 A_JumpIfInventory("Shell", 100, "NoPickup")
        TNT1 A 1 A_GiveInventory("Shell", 4)
		TNT1 A 1 A_PlaySoundEx("weapons/shotgun_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/shotgun", "Auto", 0, -1)
        Stop
    NoPickup:
        TNT1 A 1 A_PlaySoundEx("stat/armor/full", "SoundSlot6", 0, 0)
        Fail
    }
}
